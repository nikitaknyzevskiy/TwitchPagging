plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

kapt {
    generateStubs = true
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.example.twitchgametest"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def androidCore = dependencyGroup {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    //UI
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
}

def kotlinCore = dependencyGroup {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'
}

def retrofitCore = dependencyGroup {
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    // OhHttp3
    implementation "com.squareup.okhttp3:okhttp:4.4.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.4.0"
}

def roomCore = dependencyGroup {
    implementation "androidx.room:room-runtime:2.3.0-alpha02"
    kapt "androidx.room:room-compiler:2.3.0-alpha02"
    implementation "androidx.room:room-ktx:2.3.0-alpha02"
}

def paging = dependencyGroup {
    def paging_version = "3.0.0-alpha10"

    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Jetpack Compose Integration
    implementation "androidx.paging:paging-compose:1.0.0-alpha03"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    androidCore()

    kotlinCore()

    retrofitCore()

    roomCore()

    paging()
}